#!/usr/bin/env perl

use warnings;
use strict;

use Mojolicious::Lite;
use Try::Tiny;
use FindBin;
use YAML qw( LoadFile );

use Readonly;
Readonly my $ACCEPTED => 202;
Readonly my $BAD_REQUEST => 400;

use lib ("$FindBin::Bin/../lib");
use Web::Mention;

use Plerd;

my $config_ref = LoadFile( "$FindBin::Bin/../conf/plerd.conf" );
my $plerd = Plerd->new( $config_ref );


post '/' => sub {
    my $c = shift;

    my $webmention;
    try {
        $webmention = Web::Mention->new_from_request ( $c );
    }
    catch {
        $c->render( status => $BAD_REQUEST, text => "Malformed webmention: $_" );
    };
    return unless $webmention;

    my $post = $plerd->post_with_url( $webmention->target );
    unless ( $post ) {
        $c->render( status => $BAD_REQUEST, text => "Unrecognized target URL." );
        return;
    }

    my $success_text = "Webmention accepted, and queued for verification and processing. Thank you!";

    my $return_link_url = $c->param( 'target' );
    my $return_link_text = 'Return to ' . $plerd->title . '.';
    $success_text .= qq{ <a href="$return_link_url">$return_link_text</a>};

    $c->render( status => $ACCEPTED, text => $success_text );

    $plerd->webmention_queue->add_webmention( $webmention );
};


app->start;
