#!/usr/bin/env perl

use warnings;
use strict;

use Mojolicious::Lite;
use Try::Tiny;
use FindBin;
use YAML qw( LoadFile );

use Readonly;
Readonly my $ACCEPTED => 202;
Readonly my $BAD_REQUEST => 400;

use lib ("$FindBin::Bin/../lib");
use Web::Mention;

use Plerd;

my $config_ref = LoadFile( "$FindBin::Bin/../conf/plerd.conf" );
my $plerd = Plerd->new( $config_ref );


post '/' => sub {
    my $c = shift;

    my $webmention;
    try {
        $webmention = Web::Mention->new (
            source => $c->param( 'source' ),
            target => $c->param( 'target' ),
        );
    }
    catch {
        $c->render( status => $BAD_REQUEST, text => "Malformed webmention." );
    };
    return unless $webmention;

    my $post = $plerd->post_with_url( $webmention->target );
    unless ( $post ) {
        $c->render( status => $BAD_REQUEST, text => "Unrecognized target URL." );
        return;
    }

    $c->render( status => $ACCEPTED, text => "Webmention accepted. Thank you!" );

    $plerd->webmention_queue->add_webmention( $webmention );
};


app->start;
